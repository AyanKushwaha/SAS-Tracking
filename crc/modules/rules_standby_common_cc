/* -*- crc -*- 
 *
 * 
 *
 * Purpose:
 *   This module contains standby rules used common for cabin crew in Carmen 
 *   Crew Rostering and Crew Tracking
 *
 * Sections:
 *   Standby rules
 *
 * Created by:
 *   Johan Astrom, 2008-06-02
 *
 * Major changes:
 *
 */

module rules_standby_common_cc inherits rules_standby_common

import standby;
import accumulators;
import trip;
import rules;
import freedays;
import parameters;

/*
** Rule:
**    Airport standby following offduty must start no earlier than 06:00.    
**
** Description:
**    Check that airport standby following offduty starts no earlier than 06:00.    
**    The rule is valid for norwegian crew crew only.
**
** Agreement:
**    NKF/BSK K06 6.1
**
**    4ExNG - no longer applies
*/
rule sby_no_early_airport_standby_after_offduty_CC_SKN_SKL =
  valid roster.%check_rules% and
        not crew.%k4exng_cc_sp5_1_valid%(trip.%start_day%) and
        crew.%is_SKN% and
        trip.%in_pp_extended% and
        trip.%has_airport_standby% and 
        is_first(trip(wop)) and
        first(leg(trip), leg.%is_standby_at_airport%) and
        not crew.%is_leased_trip_start%;
        
  trip.%start_od% >= %valid_airport_standby_limit% - 
		    rule_exceptions.%overshoot_rel%(wop.%start_UTC%);
  startdate  = trip.%start_UTC%;
  failobject = trip.%failobject_start%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failtext(Reltime val, Reltime limit) =
    rules.%failtext_rel%("SBY: Airp sby start too early ", val, limit);
  remark "SBY: Earliest airport standby start after offday.",
  planner "Check that airport standby after offday doesnt start to early";
end

%valid_airport_standby_limit% = 6:00;

/*
** Rule: 
**    Crew must have at least 2 months of experience or
**    3 longhaul trips before performing standby.
**
** Description:
**    Crew must have at least 2 months of experience or 3
**    longhaul trips before performing standby.
**    The rule is only valid for danish longhaul cabin crew.
**
** Agreement:
**    CAU K04 B2 F1.6
*/
rule sby_2_months_lh_experience_before_sby_CC_SKD =
  valid roster.%check_rules% and
        crew.%is_SKD% and 
        crew.%has_had_new_restriction_recently% and
        wop.%in_pp_extended% and
        wop.%has_standby% and
        crew.%is_long_haul_wop_start% and
        not crew.%is_leased_wop_start% and
        rule_exceptions.%rule_on%(wop.%start_UTC%);

  %crew_legal_experience_for_standby_long_haul%;
	
  startdate = wop.%start_UTC%;
  failobject = wop.%failobject_start%;
  severity = first(leg(wop), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failtext  
    concat("SBY: Min 2 months after NEW,"  
           " valid date is: ", 
           format_time(default(%first_valid_lh_standby_date%, 31dec2035), "%02d%ub%Y"));
  remark "SBY: Crew must have at least 2 months of experience before performing standby",
  planner "The rule checks that has 2 months of experience before performing standby.";
end

%crew_legal_experience_for_standby_long_haul% =
  %crew_long_haul_experience_exceeds_2_months% or
  %crew_has_necessary_long_haul_trips%;

/*
** No NEW restricion within last 2 months
** NEW restriction ends on second familiarization flight date
*/
%crew_long_haul_experience_exceeds_2_months% =
  not crew.%has_restr_subtype_in_ival%("NEW", "ACTYPE", add_months(standby.%first_standby_in_wop_date%, -2), 
                                        standby.%first_standby_in_wop_date%);

%first_valid_lh_standby_date% =
  add_months(crew.%qrestr_new_end%(add_months(standby.%first_standby_in_wop_date%, -2),
                                   standby.%first_standby_in_wop_date%), 2);

/*
** For performance purposes we avoid specific lh trip
** accumulator and translate demand into at least 6
** lh flights
*/
%crew_has_necessary_long_haul_trips% =
  %nr_landings_since_new_enddate% >= 6;

/*
** This terrible code to avoid LH trip specific accumulator
*/
%nr_landings_since_new_enddate% =
  %nr_landing_A330_since_new_enddate% +
  %nr_landing_A340_since_new_enddate%; 

%nr_landing_A330_since_new_enddate% =
  if %lf1_330% = 01jan1986 or
     %lf1_330% < %new_restriction_date% then
    0
  else if %lf2_330% = 01jan1986 or
          %lf2_330% < %new_restriction_date% then 
    1
  else if %lf3_330% = 01jan1986 or
          %lf3_330% < %new_restriction_date% then 
    2
  else if %lf4_330% = 01jan1986 or
          %lf4_330% < %new_restriction_date% then 
    3
  else if %lf5_330% = 01jan1986 or
          %lf5_330% < %new_restriction_date% then 
    4
  else if %lf6_330% = 01jan1986 or
          %lf6_330% < %new_restriction_date% then 
    5
  else
    6;

%lf1_330% = accumulators.%last_flown%("A330", trip.%start_UTC%);
%lf2_330% = accumulators.%last_flown%("A330", %lf1_330%);
%lf3_330% = accumulators.%last_flown%("A330", %lf2_330%);
%lf4_330% = accumulators.%last_flown%("A330", %lf3_330%);
%lf5_330% = accumulators.%last_flown%("A330", %lf4_330%);
%lf6_330% = accumulators.%last_flown%("A330", %lf5_330%);

%nr_landing_A340_since_new_enddate% =
  if %lf1_340% = 01jan1986 or
     %lf1_340% < %new_restriction_date% then
    0
  else if %lf2_340% = 01jan1986 or
          %lf2_340% < %new_restriction_date% then 
    1
  else if %lf3_340% = 01jan1986 or
          %lf3_340% < %new_restriction_date% then 
    2
  else if %lf4_340% = 01jan1986 or
          %lf4_340% < %new_restriction_date% then 
    3
  else if %lf5_340% = 01jan1986 or
          %lf5_340% < %new_restriction_date% then 
    4
  else if %lf6_340% = 01jan1986 or
          %lf6_340% < %new_restriction_date% then 
    5
  else
    6;

%lf1_340% = accumulators.%last_flown%("A340", trip.%start_UTC%);
%lf2_340% = accumulators.%last_flown%("A340", %lf1_340%);
%lf3_340% = accumulators.%last_flown%("A340", %lf2_340%);
%lf4_340% = accumulators.%last_flown%("A340", %lf3_340%);
%lf5_340% = accumulators.%last_flown%("A340", %lf4_340%);
%lf6_340% = accumulators.%last_flown%("A340", %lf5_340%);

%new_restriction_date% =
  default(crew.%restriction_subtype_enddate%("NEW", "ACTYPE", standby.%first_standby_in_wop_date%), 01jan1986);

/*
** Rule: 
**    SKS temp CC may not be assigned home/hotel standby
**
** Description:
**    The rule checks that SKS temp CC are not assigned hoem/hotel standby
**    The only standby allowed is airport standby
**    
** Agreement:
**    SKS CC 
*/

rule (off) stb_only_airport_standby_allowed_temp_CC_SKS =
  valid roster.%check_rules% and
        trip.%in_pp% and
        crew.%is_SKS%  and
        crew.%is_temporary_trip_start% and
        trip.%is_standby% and
        not crew.%is_leased_trip_start%;

  trip.%is_airport_standby%;

  startdate  = trip.%start_UTC%;
  failobject = trip.%failobject_start%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failtext  
    concat("SBY: Temp CC must not be assigned ", first(leg(trip),leg.%code%)
                                                   where (leg.%is_standby%));
  remark "(CCR) SBY: Temp CC must not be assigned home/hotel standby.",
  planner "Check that SKS temp CC are not assigned home/hotel standby.";
end

/*
** Rule: 
**    Standby allowed no earlier than 4:00 local time after friday 
**    or 6:00 local time after vacation of minimum 3 days
**
** Agreement:
**    NKF K11 6.2
**
**    4ExNG - no longer applies
*/
rule stb_no_early_standby_after_freeday_vacation_skn = 
  valid roster.%check_rules% and
        not crew.%k4exng_cc_sp5_1_valid%(wop.%start_day%) and
        crew.%is_skn% and
        wop.%in_pp_extended% and
        parameters.%k11_skn_cc_sp10_9_valid%(wop.%start_day%) and
        first(trip(wop), trip.%is_standby%) and
        %wop_after_freeday_or_va%;

  wop.%start_hb% >= %sby_allowed_start_time_after_freeday_or_va% + 
					       rule_exceptions.%overshoot_rel%(wop.%start_UTC%);

  startdate = wop.%start_UTC%;
  failobject = first(duty(wop), duty.%failobject_start%);
  severity = first(leg(wop), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failtext(Abstime val, Abstime limit) = 
    concat("SBY: Standby start not before 4:00/6:00 after F-day/VA local time,"  
           "  standby allowed to start at:  ", format_time(limit,"%02d%ub%Y"));
  remark "SBY: Early standby duty not allowed after freeday or vacation",
  planner "The rule checks that standby does not start before 4:00 local time"
	        "after freeday and 6:00 local time after vacation of min 3 days.";
end

%wop_after_freeday_or_va% = default(prev(wop(chain), wop.%is_freeday% or 
                                    (wop.%is_vacation% and freedays.%vacation_length% >= 3)), false);

%sby_allowed_start_time_after_freeday_or_va% =
  wop.%start_day% + default((
  if prev(wop(chain), wop.%is_vacation% and freedays.%vacation_length% >= 3)
     where(not wop.%is_freeday%) 
  then 
    6:00 
  else 
    4:00), 4:00);

/* end of file */
