/* -*- crc -*-
 *
 * Purpose:
 *   Bid customization ratio - compare roster with a reference -> bid ratio in percent
 *
 * Sections:
 *   1. Roster cost
 *   2. Reference roster lookups
 *   3. Bid ratio calculation
 *   4. Reports definitions
 *   5. Bid selection definitions
 *
 * Created by:
 *
 * Major changes:
 *
 */
module bid_customization_ratio inherits bid_customization

import trip;
import fundamental;
import levels;
import iterators;

group dated_bid_parameters =
  %_factor_between_weights%,
  %dated_bid_cost_header%,
  %linear_coefficient%,
  %quadratic_coefficient%;

%dated_bid_cost_header% =
  parameter ""
  remark "#HEADER# Parameters for control of dated bid cost";


/****************************************************************
 * Section 1: Roster cost
 ***************************************************************/

redefine export %cost% =
 if (%factor% = 0) then 0
 else %factor% * sum(times(1), %cost_element%(%times_index_0%));

export %cost_element%(int element) =
  if (%factor% = 0) then
    0
  else
    %cost_prod_element%(element);

table cost_pbs(int element) =
  element -> export %name_element%,
             %cost_prod_element%;
  1 -> "Dated bids cost",
    %dated_bids_cost%;
  - -> "", 0;
end

export %factor% =
  parameter 10
  remark "7.0      Weight of BIDS";

%linear_coefficient% =
    parameter 150
    remark "Coefficient for linear penalty/reward when deviating from target";

%quadratic_coefficient% =
    parameter 150
    remark "Coefficient for quadratic penalty when below target";

%dated_bids_cost% =
    %cost_below_target% - %reward_above_target%;

%cost_below_target% =
    %quadratic_cost_below_target% + %linear_cost_below_target%;
%reward_above_target% =
    %div_rounds%(%linear_coefficient% * %points_above_target%, %max_roster_points%);
/* Assuming that a high points bid cannot be awarded more than 20 times */
%max_roster_points% = 20*%_high_points%;


%quadratic_cost_below_target% =
    %div_rounds%(%quadratic_coefficient% *
                 /* The code below guarantees that whatever we
                    square will stay in the [0,1] interval, so this
                    cost element will be [0,%quadratic_coefficient%]. */
                 %points_below_target% * %points_below_target%,
                 %squared_target%);
%squared_target% = %target%*%target%;

%linear_cost_below_target% =
    %div_rounds%(%linear_coefficient% *
                 /* The code below stays in the [0,1] interval, so
                    this cost element will be [0, %linear_coefficient%] */
                 %points_below_target%,
                 %target%);

%points_below_target% =
    nmax(%points_below_target_raw%, 0);

%points_above_target% =
    nmax(%points_above_target_raw%, 0);

export %crew_is_below_target% =
    %points_below_target_raw% > 0;

export %points_below_target_raw% =
    %target% - %roster_points%;

export %points_above_target_raw% =
    %roster_points% - %target%;

export %target% =
    %_high_points% + 2*%_medium_points% + 2*%_low_points%;


/****************************************************************
 * Section 3: Bid ratio calculation
 ***************************************************************/

export %ratio_sort% =
    default(fundamental.%div_rounds%(%roster_points% * 100, %target%), 0);
