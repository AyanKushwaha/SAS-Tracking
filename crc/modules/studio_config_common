/* -*- crc -*-
 *
 *
 *
 * Purpose:
 *   Specific studio variables for Carmen Crew Rostering and Tracking (rudobs, header etc).
 *
 *  The file contains:
 *  1. Header mappings
 *	2. Leg mappings
 *	3. Rudobs
 *	4. Reference Rosters
 *   .1 Non available
 *
 * Created by:
 *
 * Major changes:
 *   07-Jun-2007: Major restructuring of all studio_config modules.
 *                /Erik Gustafsson
 *
 */

module studio_config_common inherits studio_config
import rule_exceptions;
import crew;
import pp;
import annotations;
import qualification;
import freedays;
import roster;
import training;

/*******************************************************************
**
** Section 1: Header mappings
**
********************************************************************/

/*
** Crew header
*/
/* Text */
%crew_header_text_top_left% = 
  default(crew.%login_name%,"No name");

%crew_header_text_top_middle% = "";
%crew_header_text_top_right% = 
  default(crew.%rank%,"");

%crew_header_text_bottom_left% = 
  default(crew.%employee_number%,     
	  default(crew.%id%,"No empl. nr"));
                                          
%crew_header_text_bottom_middle% = 
  if default(crew.%special_qual_pp_start%, "") <> ""
  then
    crew.%special_qual_pp_start%
  else
    if default(crew.%_spec_sched_limit_int_to%("PartTime", crew.%available_start%),0) > 0
    then
       concat(format_int(crew.%crew_contract_part_time_factor_at_date%(crew.%available_start%),"%i"),"*")
    else
       format_int(crew.%crew_contract_part_time_factor_at_date%(crew.%available_start%),"%i");  

%crew_header_text_bottom_right% = 
  default(crew.%homebase%,"");

/* Colors */
%color_crew_header% = %color_default%;
%color_crew_header_top% = 
  if %rulefailure% then 
    %color_illegal% 
  else
    %color_crew_header%;

%crew_header_color_top_left% = %color_default%;
%crew_header_color_top_middle% = %color_default%;
%crew_header_color_top_right% = 
  if rule_exceptions.%crew_has% then 
    %color_rule_exception%
  else 
    %color_default%;
    
%crew_header_color_bottom_left%	= 
  if annotations.%on_crew% then 
    %color_annotation%
  else
    %color_crew_header%; /*28  This is the default background colour */
%crew_header_color_bottom_middle% = %color_crew_header%; /*28 /* This is the default background colour */
%crew_header_color_bottom_right% = %color_crew_header%; /*28 /* This is the default background colour */

 
/*******************************************************************
**
** Section 2: Leg mappings
**
********************************************************************/

/* Redefinition of task code visualisation in studio to handle PC/OPC code conversions */
%leg_text_inside_top_crew% = 
  default(training.%leg_code_redefined%, "*");

%leg_color_top_crew% = %leg_color%;
%leg_color_middle_left_crew% = %leg_color_middle_crew%;
%leg_color_middle_right_crew% = %leg_color_middle_crew%;
%leg_color_middle_crew% = %leg_color_middle%;
%leg_color_bottom_crew% = %leg_color%;
redefine %leg_color_middle_crr% = %leg_color_middle%;

%leg_color_bottom_left_crew% =
  if leg.%is_flight_duty% and not leg.%is_deadhead% and crew_pos.%above_or_below% then
    if crew_pos.%higher_rank% then 2
    else 25
  else %leg_color%;
  
/* AC-qual */
%rudob_display_leg_ac_qual% =
  parameter False
  remark "Show markers for AC-qualification";

set leg_ac_qual_gr1 =
  parameter "A3", "A4", "AL"
  remark "AC-qualification color 1";

set leg_ac_qual_gr2 =
  parameter "36", "37", "38"
  remark "AC-qualification color 2";

/*Used in the customized copy of PACT's (SASCMS-5416)*/
%leg_is_selected% = %selected% = leg_identifier;
%selected% =
    parameter -1
    remark "Sets during copy of PACT";

/* LR & A320 check added in SKCMS-2392 */
%leg_color_middle% =
  if %rudob_display_leg_ac_qual% and
    (leg.%is_active_flight% or
    leg.%is_standby% or
    leg.%is_simulator%) and
    not leg.%is_nop% then
    /* If the qual is in group 1 we use the default color */
     if leg.%ac_version% = "LR" then
       11
     else if leg.%qual% = "A2" then
       14
     else if leg.%qual% in leg_ac_qual_gr1 then
       27
     else if leg.%qual% in leg_ac_qual_gr2 then
       31
     else
       %leg_color%
  else if %leg_is_selected% then
    1
  else
    %leg_color%;

/*******************************************************************
**
** Section 3: Rudobs
**
********************************************************************/
 
/*******************************************************
 * 3.1 Non available
 *******************************************************/
%rudob_non_available_display% = 
  parameter True
  remark "Show markers for non available periods"; 

%rudob_non_available_carry_in_start% = crew.%utc_time%(%pp_start%);
%rudob_non_available_carry_in_len% = 
  if %rudob_non_available_display%  then
    crew.%utc_time%(crew.%available_start%) - %rudob_non_available_carry_in_start%
  else 
    0:00;

%rudob_non_available_carry_out_start% =
  crew.%utc_time%(%_rudob_non_available_carry_out_start%);
  
%_rudob_non_available_carry_out_start% =
  let crew_end =
    default( crew.%last_employment_end% ,1Jan1986);
  if crew_end > 1jan1986 and crew_end <= fundamental.%plan_end%
  then
      crew_end
  else if crew.%available_end% = %pp_end%
  then
      fundamental.%plan_end%
  else
      crew.%available_end%;
  
%rudob_non_available_carry_out_len% = 
  if %rudob_non_available_display%  then
    nmax(00:00,%rudob_date_range%(crew.%utc_time%(fundamental.%plan_end%)) - 
               %rudob_non_available_carry_out_start%)
  else 
    0:00;

%rudob_non_available_text% = "Non available period";
%rudob_non_available_color% = %color_non_available%;
%rudob_non_available_bottom% = 0; 
%rudob_non_available_top% = 100;
%rudob_non_available_layer% = 0;

/*******************************************************************
**
** Section 3.2: Adhoc on loan to other region
**
********************************************************************/

%rudob_adhoc_on_loan_p% =
  parameter true
  remark "Show days crew is on adhoc loan to other region.";

%rudob_adhoc_on_loan_display% =
  %rudob_adhoc_on_loan_p% and not %is_reference_roster_view% and
  crew.%is_on_ad_hoc_loan_at_date%(trip.%start_day%);

%rudob_adhoc_on_loan_start% =
  trip.%start_day% - 1:00;

%rudob_adhoc_on_loan_length% =
  if %rudob_adhoc_on_loan_display% then
    trip.%days% * 24:00
  else
    0:00;

%rudob_adhoc_on_loan_bg_colour% = 
  %color_adhoc_on_loan_bg%;

%rudob_adhoc_on_loan_text% =
  concat("Adhoc loan to ",
         crew.%region_on_loan_to_at_date%(trip.%start_day%));

%rudob_adhoc_on_loan_bottom% = 0;
%rudob_adhoc_on_loan_top% = 100;
%rudob_adhoc_on_loan_delim% = false;             
%rudob_adhoc_on_loan_layer% = 0;               

/*******************************************************************
**
** Section 4: Reference Rosters
**
********************************************************************/
 
%is_reference_roster_view% = first(leg(roster), reference_plan_activity);
%name_reference_plan% = 
  if %is_reference_roster_view%
    then first(leg(roster), reference_plan_name)
    else "";
 
/*******************************************************************
**
** Section 5: Non-released course objects
**
********************************************************************/

%rudob_show_unreleased_courses_p% = 
  parameter true
  remark "Show markers for courses not released yet";
  
%rudob_non_released_course_length%(Int nr) = 
  if %rudob_show_unreleased_courses_p% and is_first(leg(chain)) then
    default(training.%non_relesed_course_end%(nr)-
            training.%non_relesed_course_start%(nr), 0:00)
  else 
    0:00;

%rudob_non_released_course_start%(Int nr) = 
  if is_first(leg(chain)) then 
    default(crew.%utc_time%(training.%non_relesed_course_start%(nr)), 1jan1986)
  else
    1jan1986;

%rudob_non_released_course_text%(Int nr) = 
  concat("Course ", training.%non_relesed_course_name%(nr), 
         " created in Manpower");

%rudob_non_released_course_color% = 9;
%rudob_non_released_course_delim% = true;
%rudob_non_released_course_layer% = 0;
%rudob_non_released_course_top% = 25;
%rudob_non_released_course_bottom% = 5;

%rudob_non_released_course_length_1% = %rudob_non_released_course_length%(1);
%rudob_non_released_course_start_1% = %rudob_non_released_course_start%(1);
%rudob_non_released_course_text_1% = %rudob_non_released_course_text%(1);
%rudob_non_released_course_length_2% = %rudob_non_released_course_length%(2);
%rudob_non_released_course_start_2% = %rudob_non_released_course_start%(2);
%rudob_non_released_course_text_2% = %rudob_non_released_course_text%(2);
%rudob_non_released_course_length_3% = %rudob_non_released_course_length%(3);
%rudob_non_released_course_start_3% = %rudob_non_released_course_start%(3);
%rudob_non_released_course_text_3% = %rudob_non_released_course_text%(3);
%rudob_non_released_course_length_4% = %rudob_non_released_course_length%(4);
%rudob_non_released_course_start_4% = %rudob_non_released_course_start%(4);
%rudob_non_released_course_text_4% = %rudob_non_released_course_text%(4);
%rudob_non_released_course_length_5% = %rudob_non_released_course_length%(5);
%rudob_non_released_course_start_5% = %rudob_non_released_course_start%(5);
%rudob_non_released_course_text_5% = %rudob_non_released_course_text%(5);

/*******************************************************
 * FAM FLT needed rudob
 *******************************************************/
%rudob_fam_flt_needed_display_p% = True; /*False;*/

%rudob_fam_flt_needed_display% =
  %rudob_fam_flt_needed_display_p%;

%rudob_fam_flt_needed_start% =
  %rudob_date_range%(crew.%utc_time%(training.%fam_flt_needed_start%));
%rudob_fam_flt_needed_end% =
  %rudob_date_range%(crew.%utc_time%(training.%fam_flt_needed_end%));
%rudob_fam_flt_needed_len% =
  if %rudob_fam_flt_needed_display% and training.%fam_flt_needed% then
     default(nmax(0:00, %rudob_fam_flt_needed_end% - %rudob_fam_flt_needed_start%), 0:00)
  else 0:00;
%rudob_fam_flt_needed_color% = 8; /* Blue */
%rudob_fam_flt_needed_bottom% = 0;
%rudob_fam_flt_needed_top% = 25;
%rudob_fam_flt_needed_delim% = False;
%rudob_fam_flt_needed_layer% = 0;
%rudob_fam_flt_needed_text% =
  concat(format_time(round_down(training.%fam_flt_needed_end% - 0:01, 24:00), "FAM FLT needed latest %02d%b%Y"),
         " (", training.%fam_flt_needed_ac_qual%, ")");


/*******************************************************
 * ETOPS LIFUS/LC needed rudob
 *******************************************************/
%rudob_etops_lifus_lc_needed_display_p% = True;

%rudob_etops_lifus_lc_needed_display% =
  %rudob_etops_lifus_lc_needed_display_p%;

%rudob_etops_lifus_lc_needed_start% =
  %rudob_date_range%(crew.%utc_time%(training.%etops_lifus_lc_needed_start%));
%rudob_etops_lifus_lc_needed_end% =
  %rudob_date_range%(crew.%utc_time%(training.%etops_lifus_lc_needed_end%));
%rudob_etops_lifus_lc_needed_len% =
  if %rudob_etops_lifus_lc_needed_display% and training.%etops_lifus_lc_needed% then
     default(nmax(0:00, %rudob_etops_lifus_lc_needed_end% - %rudob_etops_lifus_lc_needed_start%), 0:00)
  else 0:00;
%rudob_etops_lifus_lc_needed_color% = 8; /* Blue */
%rudob_etops_lifus_lc_needed_bottom% = 0;
%rudob_etops_lifus_lc_needed_top% = 25;
%rudob_etops_lifus_lc_needed_delim% = False;
%rudob_etops_lifus_lc_needed_layer% = 0;
%rudob_etops_lifus_lc_needed_text% =
  concat(format_time(round_down(training.%etops_lifus_lc_needed_end% - 0:01, 24:00), "ETOPS LIFUS/LC needed latest %02d%b%Y"),
         " (", training.%etops_lifus_lc_needed_ac_qual%, ")");
/* end of file */
