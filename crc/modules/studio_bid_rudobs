/* -*- crc -*-*/
module studio_bid_rudobs

import bid;
import crew;
import fundamental;
import studio_config;
import time_utils;
import trip;

/*
 * FUNCTIONS CALLED WITH RUDOB NO
 */

/* TimeOff bids */
export %get_timeoff_rudob_top%(Int bogus) = 100;

export %get_timeoff_rudob_bottom%(Int bogus) = 92;

export %get_timeoff_rudob_color%(Int bid_idx) =
    let bidl = bid.%timeoff_line%(bid_idx);
    if bid.%non_granted_bid_in_roster%(bidl) then
        studio_config.%color_violated_bid%
    else if bid.%is_high_bid%(bidl) then
        studio_config.%color_granted_high_bid%
    else
        studio_config.%color_granted_bid%;

export %get_timeoff_rudob_text%(Int bid_idx) =
    concat("Time Off bid: ",
           %abs2str%(bid.%timeoff_abs1%(bid_idx)),
           " - ",
           %abs2str%(bid.%timeoff_abs2%(bid_idx)));

export %get_timeoff_rudob_len%(Int bid_idx) =
    let bidl = bid.%timeoff_line%(bid_idx);
    if studio_config.%roster_bid_rudob_display_p% and
       bid.%is_timeoff_bid%(bidl) and
       bid.%is_valid%(bidl) and
       not void(bid.%timeoff_abs1%(bid_idx)) then
            bid.%timeoff_abs2%(bid_idx) - bid.%timeoff_abs1%(bid_idx)
    else
        0:00;

export %get_timeoff_rudob_start%(Int bid_idx) =
    %hb2utc%(bid.%timeoff_abs1%(bid_idx));


export %trip_grants_any% = bid.%trip_grants_any%;
export %trip_violates_any% = bid.%trip_violates_any%;


/******************************************
 *              Trip bid rudobs           *
 * Covers Flight, Stop, CheckIn, CheckOut *
 ******************************************/

%display_trip_bid%(int ix) =
    bid.%is_production_trip% and
    trip.%in_pp% and
    bid.%is_valid%(ix) and
    bid.%trip_bid_granted%(ix);

export %trip_bid_rudob_len%(int ix) =
    if %display_trip_bid%(ix) then
        trip.%end_utc% - trip.%start_utc%
    else
        0:00;

export %trip_bid_rudob_start%(int ix) = trip.%start_utc%;

export %trip_bid_rudob_color%(int ix) =
    if bid.%is_high_bid%(ix) then
        studio_config.%color_granted_high_bid%
    else
        studio_config.%color_granted_bid%;

table trip_bid_rudob_text(int ix) =
    bid.%type%(ix, 0) -> export string %trip_bid_rudob_text%;
      "Flight" -> concat("Granted ", bid.%points_text%(ix), " Flight bid: ", bid.%str1%(ix, 0));
        "Stop" -> concat("Granted ", bid.%points_text%(ix), " Stop bid in: ", bid.%str1%(ix, 0));
     "CheckIn" -> concat("Late Check In ", bid.%points_text%(ix), " bid granted. Check In after ",
                        format_time(bid.%rel1%(ix, 0), "%02H:%02M"));
    "CheckOut" -> concat("Early Check Out ", bid.%points_text%(ix), " bid granted. Check Out before ",
                         format_time(bid.%rel2%(ix, 0), "%02H:%02M"));
             - -> "Unexpected bid type!";
    end

export %trip_bid_rudob_top%(int ix) = 100;
export %trip_bid_rudob_bottom%(int ix) = 90;

