/* -*- crc -*-
 *
 * Purpose:
 *   This module contains common absence definitions for flight and cabin crew
 *   used in absence refresher rules, costs and select filters.
 *
 * Sections:
 *   1. Fundamentals
 *   2. Functions
 *   3. Variables
 *   4. Failtexts & crew warnings
 *
 * Created by:
 *   Henrik Albertson, 30apr2007
 *
 * Major changes:
 */

root module absence

import fundamental;
import levels;
import leg;
import duty;
import trip;
import crew;
import crew_pos;
import training;
import recency;

/****************************************************************
 * Section 1: Fundamentals
 ***************************************************************/
/*
** Basic definitions
*/
/* For flight crew and absence up to hard_limit there is no requirement
 * for ground training, only flight training will be checked. 
 * However, for SAS aoc and absence 46-120 days either ground training or 
 * flight training is required. */
table absence_limits(String aoc) =
  %flight_crew%, aoc ->
    Int %_limit%,
    Int %_hard_limit%,
    Int %_max_days_from_ground_training%;
  true,  "SAS" -> 90,  120, 90; /* Agreement: OM-A 5.2.10 table 2 row 1, note 1 */
  true,  "BU"  -> 90,  120, 90;
  false, - -> 90,  90,  14;
  -, - -> 0, 0, 0;
end

/****************************************************************
 * Section 2: Functions
 ***************************************************************/
/*
** General functions
*/

/*
** Functions redefined in child modules
*/
/* Chain dependent */
%type_at_date%(String ac_qual, Abstime date) = String;
%_reqd_flight_training_legs%(String ac_qual, Abstime date) = Int;

/* Leg dependent */
%_reqd_flight_training_type%(String ac_qual, Abstime date) = Bool;

/*
** Rule check functions
*/
/* Trip dependent */
%_ground_training_date%(String ac_qual, Abstime date) = Abstime;

/* Leg dependent */
%_nr_flight_training_legs%(String ac_qual, Abstime date) = Int;

/****************************************************************
 * Section 3: Variables
 ***************************************************************/
/*
** Cahce variables (leg dependent)
*/

%aoc% = recency.%aoc_for_ac_fam%(leg.%ac_family%);

export %absence_sim% = False;

export %limit% = %_limit%(%aoc%);
export %hard_limit% = %_hard_limit%(%aoc%);

export %type% = %type_at_date%(leg.%qual%, leg.%start_UTC%);

/* Rule check variables */
export %reqd_flight_training_legs% =
  %_reqd_flight_training_legs%(leg.%qual%, leg.%start_UTC%);

export %nr_flight_training_legs% =
  %_nr_flight_training_legs%(leg.%qual%, leg.%start_UTC%);

export %ground_training_date_leg_start% =
  %ground_training_date%(leg.%qual%, leg.%start_date%);

%ground_training_date%(String qual, Abstime date) =
  default(%_ground_training_date%(qual, date), 01jan1986);

export %max_days_from_ground_training% = %_max_days_from_ground_training%(%aoc%);

/****************************************************************
 * Section 4: Failtexts & crew warnings
 ***************************************************************/

%reqd_gnd_training_str_at_date%(String ac_qual, Abstime date) = String;
%reqd_flt_training_str_at_date%(String ac_qual, Abstime date) = String;
    
export %_check_qual%(String qual) =
    qual = leg.%qual% or
    qual = "37" and leg.%qual% = "38" or
    qual = "38" and leg.%qual% = "37";

/*
** Cache variables (leg dependent)
*/
  
export %reqd_flt_training_str% =
  %reqd_flt_training_str_at_date%(leg.%qual%, leg.%start_UTC%);

export %reqd_gnd_training_str% =
  %reqd_gnd_training_str_at_date%(leg.%qual%, leg.%start_UTC%);

export %reqd_training_str_at_date%(String ac_qual, Abstime date) = String;

/* end of file */

