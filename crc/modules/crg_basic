/* -*- crc -*- 
 *
 * 
 *
 * Purpose:
 *   This module contains common definitions for reports
 *
 * Created by:
 *   Extracted from the Carmen Standard User, 07-Dec-2005
 *
 * Major changes:
 *
 */

module crg_basic
import fundamental;
import levels;
import leg;
import duty;

global export enum EnumTimeMode = 
  timemode_LT remark "Local"; 
  timemode_UTC remark "UTC"; 
  timemode_HB remark "Home base";
  remark "Time base";
end



/* Config item */
%thousand_separator% = " ";

%figure_format_string% = concat("%w3",%thousand_separator%,"d");
export %print_figure%(Int i) = format_int(i, %figure_format_string%);

export enum PaperSize = A4; Letter; end
export %paper_size% = A4; /* change also in common_defs.inc */
export %extra_days% = if %paper_size% = crg_basic.Letter
                      then 3 else 5;

/*
** Standard presentation of common attributes
*/

%flight_nr_string% = 
  if NOT leg.%is_flight_duty% then leg.%code%
  else format_int(leg.%flight_nr%,"%03d");

%flight_suffix% = 
  if NOT leg.%is_flight_duty% then " "
  else leg.%flight_suffix_padded%;

%leg_number% = 
  if leg_number = 1 then "" else format_int(leg_number,"-%d");

export %dh_mark% = 
  if leg.%is_oag% then leg.%flight_carrier%
  else if leg.%is_ground_transport% then "GT"
  else if leg.%is_deadhead% then "DH"
  else "  ";

/*
** Planning period
*/

export %is_daily_plan% = 
  not is_dated_mode and
  round_down(lp_period_start, 24:00) = round_down(lp_period_end, 24:00);

export %report_pp_start% = 
  if not is_dated_mode then lp_period_start
  else if %pp_start% = 01Jan1986 then %fallback_pp_start%
  else %pp_start%;

export %report_pp_end% = 
  if not is_dated_mode then lp_period_end
  else if %pp_start% = 01Jan1986 then %fallback_pp_end%
  else %pp_end%;

export %report_pp_days% = (%report_pp_end% - %report_pp_start%) / 24:00;

/* Fallback code if PP is not set. Should we have this???, HenrikA 09-Dec-2005 */
%middle_of_lp_period% = 
  lp_period_start + ((lp_period_end - lp_period_start) / 2);
%fallback_pp_start% = round_down_month(%middle_of_lp_period%);
%fallback_pp_end% = round_up_month(%middle_of_lp_period% + 0:01);

/*
** Misc
*/

/* Things that cannot be expressed in CRG */
export %rule_failure% = default(rulefailure, false);
%leg_is_first_in_duty% = leg.%is_first_in_duty%;
%leg_is_last_in_duty% = leg.%is_last_in_duty%;
export %leg_is_first_in_trip% = leg.%is_first_in_trip%;
export %leg_is_last_in_trip% = leg.%is_last_in_trip%;
%duty_is_first_in_trip% = is_first(duty(trip));
%duty_is_last_in_trip% = is_last(duty(trip));

/* Probably obsolete in SAS CMS ???, HenrikA 09-Dec-2005 */
/* Workarounds because curly braces does not work in the
   CRG preprocessing (m4) */
export %curly_left_brace% = "{";
export %curly_right_brace% = "}";

/* end of file */

