/* -*- crc -*- 
 *
 * 
 *
 * Purpose:
 *   Definitions of tracking specific trip fundamentals.
 *
 * Created by:
 *   Peter Jonsson 28 feb 2006
 *
 * Major changes:
 *
 */

module trip_cct inherits trip_ccr
import duty;
import leg;

redefine %group_code_breaks_wop% = not (%group_code% in no_wop_break_group_codes);
redefine %task_code_breaks_wop% = not (%code% in no_wop_break_task_codes);

redefine %illness_breaks_wop% =
  ((%is_on_duty_illness% or %is_on_duty%) and
   next(trip(chain), %is_off_duty_illness%)) or
  (%is_off_duty_illness% and
   next(trip(chain), %is_on_duty_illness% or
                     %is_on_duty%));

redefine export %is_single_leg_standby_callout% =
  %num_duties% = 1 and
  first(duty(trip), duty.%is_single_leg_standby_callout%);

redefine export %has_standby_callout% =
  any(duty(trip), duty.%is_standby_callout%);
  
export %is_BLS% =
  first(leg(trip), leg.%is_BLS%);
  
redefine export %is_sby_manual_duty_break% =
  trip.%num_legs% = 1 and 
  first(leg(trip), leg.%is_sby_manual_duty_break%);
                                
redefine export %has_delayed_check_out% = 
  any(duty(trip), duty.%has_delayed_check_out%); 
 
/* Containing not operating legs.*/
export %has_not_operating_leg% =
  any(duty(trip), duty.%has_not_operating_leg%);

export %is_operating% =
  any(duty(trip), duty.%is_operating%);

export %starts_at_homebase% = 
  first(leg(trip), leg.%departure_airport_base%) = trip.%crew_homestation%;
export %ends_at_homebase% = 
  last(leg(trip), leg.%arrival_airport_base%) = trip.%crew_homestation%;

export %last_leg_has_landed% =
  last(duty(trip),duty.%last_leg_has_landed%);

export %prev_trip_start_utc% =
  prev(trip(roster), trip.%start_utc%);

export %next_trip_end_utc% =
  next(trip(roster), trip.%end_utc%);


/****************************************************************
 *  Duty time redefinitions
 ***************************************************************/
  
redefine export %duty_time_in_period_scheduled%(Abstime a1, Abstime a2, Bool night_upg, DutyCalculation dutycalc, Bool include_privately_traded) =
  if %end_scheduled_UTC% <= a1 or %start_scheduled_UTC% >= a2 then 
    0:00
  else
    sum(duty(trip), duty.%duty_time_in_period_scheduled%(a1, a2, night_upg, dutycalc))
    where (not (include_privately_traded and duty.%is_privately_traded%));


/* end of file */

