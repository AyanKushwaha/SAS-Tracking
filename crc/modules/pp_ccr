/* -*- crc -*- 
 *
 * Purpose:
 *   This file contains the additional planning period definitions used in Carmen Crew Rostering.
 *
 * Created by:
 *   Extracted from the Carmen Standard User, 07-Dec-2005
 *
 * Major changes:
 *
 */

module pp_ccr inherits pp
import levels;
import crew;
import wop;
import trip;
import duty;

/*
** Basic definitions
*/

export %crew_is_plannable% = 
  default(crew.%is_plannable%/* and %plannable_days% > 0*/,true);

/* Left the code for possibly use in the future, HenrikA 06-Dec-2005 */
/* Impossible to implement correctly, at least without info about DST...
%start_UTC% =
%end_UTC% = 
*/

/* Time away from base */
export %tafb% =
  sum(trip(roster),trip.%time%)
  where(trip.%has_only_flight_duty%);

export %duty_days% =
  sum(wop(roster),wop.%pp_days%) where (wop.%is_on_duty%);
export %duty_days_in_published% =
  sum(wop(roster),wop.%publ_period_days%) where (wop.%is_on_duty%);
export %rest_days% = %days% - %duty_days%;
export %rest_days_in_published% = 
  %days_in_published% - %duty_days_in_published%;
export %non_rest_days% = %duty_days%;

export %deadheads% = 
  sum(trip(roster),trip.%pp_deadheads%) where (trip.%has_only_flight_duty%);
export %early_starts% = 
  sum(trip(roster),trip.%pp_early_starts%) where (trip.%is_on_duty%);
export %night_duties% = 
  sum(trip(roster), trip.%pp_night_duties%) where (trip.%has_only_flight_duty%);

export %standbys% = 
  sum(trip(roster), trip.%pp_standbys%);
export %standby_days% = 
  sum(trip(roster), trip.%pp_standby_days%);

export %duty_time% = 
  sum(wop(roster), wop.%duty_time_in_period%(crew.%utc_time%(%pp_start%),
                                             crew.%utc_time%(%pp_end%),
                                             fundamental.%night_upg%, 
                                             duty.union));
export %duty_time_no_night_upg% = 
  sum(wop(roster), wop.%duty_time_in_period%(crew.%utc_time%(%pp_start%), 
                                             crew.%utc_time%(%pp_end%), false, 
                                             duty.union));
export %duty_time_in_published% = 
  sum(wop(roster), wop.%duty_time_in_period%(crew.%utc_time%(%pp_start%),
                                             crew.%utc_time%(%publ_period_end%), 
                                             fundamental.%night_upg%, 
                                             duty.union));

export %block_time% = sum(trip(roster),trip.%pp_block_time%);
export %credit_time% = 0:00;

/* Layovers in station */
/* Bad name! pp unnecessary in name..., HenrikA 06-Dec-2005 */
export %pp_layovers_in_station%(String station) =
  count(duty(roster)) 
  where (station = duty.%layover_station% and 
	  trip.%has_only_flight_duty% and
	  not trip.%is_simulator% and 
	  duty.%in_pp% and 
	  not is_last(duty(trip)));

export %plannable_days% = %days% - %locked_days%;
export %locked_days% =
  sum(wop(roster), wop.%pp_days%) where (wop.%is_locked%);

/* end of file */

