/* -*- crc -*- 
 * 
 * 
 *
 * Purpose:
 *   This module contains flight crew only government rules from the Civil Avaiation
 *   Authority to be used in Carmen Crew Rostering and as studio only 
 *   in Carmen Crew Tracking.
 *
 * Sections:
 *   1 Aircraft qualification rules
 *
 * Created by:
 *   Henrik Mouritsen, 12-Oct-2006
 *
 * Major changes:
 *
 */
root module rules_caa_ccp_fc

import trip;
import duty;
import leg;
import levels;
import rule_exceptions;
import fundamental;
/****************************************************************
 * Section 1: Aircraft qualification rules
 ***************************************************************/

/*
** Rule:
**    Multi qualified pilot (A3A4(A5)) must only fly one type in duty
**
** Description:
**    Check that a multi qualified pilot only flies one type
**    in a duty pass.
**
** Agreement:
**    OM-A 5.2.7.5
*/
export rule qln_max_one_ac_type_in_duty_for_double_qual_FC =
  valid trip.%check_rules% and
        duty.%in_pp_extended% and
        duty.%with_long_haul_ac% and
	rule_exceptions.%rule_on%(duty.%start_UTC%);
  not %duty_has_ac_family_change%;
  startdate  = duty.%start_UTC%;
  severity = first(leg(duty), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failobject = %qln_max_one_ac_type_in_duty_for_double_qual_FC_failobject%;
  failtext "OMA: A3A4A5 only one ac-type/duty";
  remark "OMA: A3A4(A5) flight crew must only fly one type in a duty",
  planner "The rule checks that a multi qualified pilot only"
          " flies one type in a duty pass.";
end

%qln_max_one_ac_type_in_duty_for_double_qual_FC_failobject% =
  first(leg(duty), next(leg(duty),leg.%failobject_departure%)) 
    where (%leg_ac_family_change%);
%duty_has_ac_family_change% = any(leg(duty), %leg_ac_family_change%);
%leg_ac_family_change% =
  leg.%is_active_flight% and
  default(leg.%ac_family% <> 
          next(leg(duty), leg.%ac_family%) where (leg.%is_active_flight%),
  false);

/* end of file */
