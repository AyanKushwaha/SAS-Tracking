/* -*- crc -*- 
 *
 * Purpose:
 *   This file contains the additional level definitions 
 *   required for Carmen Crew Rostering. The same level
 *   definitions will also be active in Tracking.
 * Created by:
 *   Extracted from the Carmen Standard User, 07-Dec-2005
 * Major changes:
 *
 */
module levels_ccr inherits levels_ccp
import trip;
import short_ground_training;

/*
** Duty level
**
** Redefinition of duty level to allow the inclusion of short ground training
** activities immediately before or after the last duty in a trip
*/

redefine %levels_leg_is_last_in_duty% =
    %legs_separated_by_duty_rest% or
    ((next(leg(chain), trip_id <> -1) and next(leg(chain), trip_id) <> trip_id) and
     /* Do not break pacts that are on the same day */
     not (%leg_is_short_duty_on_ground% and
          next(leg(chain), %leg_is_short_duty_on_ground%)) and
     /* Do not break on an SGT on the same day */
     not (short_ground_training.%SGT_leg% or
          next(leg(chain), short_ground_training.%SGT_leg%)));

/*
** Trip level
**
** Corresponds to the Studio definition, which is strongly recommended
*/

%duty_trip_id% = first(leg(duty),trip_id);
%duty_trip_id_last% = last(leg(duty),trip_id);
%levels_duty_is_last_in_trip% =
  %duty_trip_id_last% <> next(duty(roster),%duty_trip_id%);

redefine global export level trip = 
  is_last(duty)
  when(%levels_duty_is_last_in_trip%);
end

/*
** Wop level
**
** The wop level is used when checking rules which span over one or
** more consecutive trips. Each wop should be preceded and followed by
** valid freedays.
*/

redefine %levels_trip_is_last_in_wop% = trip.%levels_trip_is_last_in_wop%;

redefine global export level wop =
  is_last(trip)
  when(%levels_trip_is_last_in_wop%);
end

/*
** Roster level
*/

global export level roster = levels.chain;
global export level trip_defined_in_ruleset = trip;

/* end of file */
