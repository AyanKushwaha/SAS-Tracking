/* -*- crc -*-
 * 
 *
 * Purpose:
 *
 * Created by:
 *   Stefan Hansson, 19-Jun-2007
 *
 * Major changes:
 *
 */

module rules_resched_cct_fc inherits rules_resched_cct_mainline

import crew;
import levels;
import leg;
import duty;
import trip;
import rescheduling;
import rule_exceptions;
import standby;
import parameters;
import calendar;
  
/*******************************************************************************
* NOTIFICATION DEADLINE
*******************************************************************************/

redefine %trip_notification_deadline_FC% =
  nmin(
       default(%trip_notification_deadline_earlier_stb_within_48h_from_ci%, 31Dec2099),
       default(%trip_notification_deadline_earlier_stb_end_ok%, 31Dec2099),
       default(%trip_notification_deadline_sby_callout_ends_late%, 31Dec2099),
       default(%trip_notification_deadline_bl_resched%, 31Dec2099));
 
/*******************************************************************************
* RULES
*******************************************************************************/

/****************************************************************
 * Section 1: Standby-duty rules
 *
 * These rules are only dependent on the current roster
 ***************************************************************/

   
/*******************************************************************/

/*
** Rule: R403 & R404
*/

%_earlier_stb_within_48h_from_ci_inform_in_time_valid% =
  crew.%is_long_haul_trip_start%
  and not crew.%is_leased_trip_start%
  and is_first(trip(wop))
  and %trip_is_cancellation_standby%
  and rescheduling.%trip_inf_any_production%;

%earlier_stb_within_48h_from_ci_inform_in_time% =
  rescheduling.%trip_inf_checkin% - %now_hb% >= 48:00;
  
/*
** Rule: R403
*/
  
%resched_earlier_stb_within_48h_from_ci_valid% =
  %_earlier_stb_within_48h_from_ci_inform_in_time_valid%
  and rescheduling.%trip_has_rescheduled_checkin%
  and rescheduling.%trip_checkin_hb% < rescheduling.%trip_inf_checkin%;
    
rule resched_earlier_stb_within_48h_from_ci_FC =
  valid %resched_earlier_stb_within_48h_from_ci_valid%
	  and rule_exceptions.%rule_on%(trip.%start_UTC%);

  %earlier_stb_within_48h_from_ci_inform_in_time%;

  startdate = trip.%start_UTC%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failobject = trip.%failobject_start%;
  failtext  "Coll: Earlier sby start informed <48h in advance";
  remark "(CCT) Resched: Earlier standby start when informed late",
  planner "The rule checks that when replacing cancelled production by standby"
          " less than 48 hours before the planned check-in, standby must"
          " not start earlier than local transfer start before the cancelled"
          " production."
          " The rule is only valid for FCLH.";
end

%trip_notification_deadline_earlier_stb_within_48h_from_ci% =
  if %resched_earlier_stb_within_48h_from_ci_valid%
  then rescheduling.%trip_inf_checkin% - 48:00
  else void_abstime;

/*
** Rule: R404
*/
  
%resched_earlier_stb_within_48h_from_ci_end_ok_valid% =
  %_earlier_stb_within_48h_from_ci_inform_in_time_valid%
  and rescheduling.%trip_has_rescheduled_checkout%
  and not rescheduling.%wp_fat_legality_500_trip_inf_checkout_is_suspicious%
  and rescheduling.%trip_scheduled_checkout_hb% > rescheduling.%trip_inf_checkout%;
    
rule resched_earlier_stb_within_48h_from_ci_end_ok_FC =
  valid %resched_earlier_stb_within_48h_from_ci_end_ok_valid%
	  and rule_exceptions.%rule_on%(trip.%start_UTC%);
    
  %earlier_stb_within_48h_from_ci_inform_in_time%;
  
  startdate = trip.%start_UTC%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failobject = trip.%failobject_start%;
  failtext  "Coll: Later sby end informed <48h in advance";  
  remark "(CCT) Resched: Later standby end when informed late",
  planner "The rule checks that when replacing cancelled production by standby"
          " less than 48 hours before the planned check-in, standby must"
          " not end later than the published end of the cancelled production."
          " The rule is only valid for FCLH.";
end

%trip_notification_deadline_earlier_stb_end_ok% =
  if %resched_earlier_stb_within_48h_from_ci_end_ok_valid%
  then rescheduling.%trip_inf_checkin% - 48:00
  else void_abstime;

/*
** Rule: R405
*/
  
%resched_sby_callout_ends_too_late_valid% =
  not crew.%is_leased_duty_start%
  and rescheduling.%duty_is_long_haul%
  and standby.%duty_is_standby_callout%
  and duty.%end_scheduled_hb% > rescheduling.%trip_inf_checkout% + 2:00
  and not rescheduling.%wp_fat_legality_500_trip_inf_checkout_is_suspicious%
  and rescheduling.%duty_inf_rescheduled_from_prod_to_standby_late%;
  
rule resched_sby_callout_ends_too_late_FC =
  valid %resched_sby_callout_ends_too_late_valid%
	  and rule_exceptions.%rule_on%(duty.%start_UTC%);
    
  rescheduling.%duty_inf_rescheduled_from_prod_to_standby_late%;
  
  startdate = duty.%start_UTC%;
  severity = first(leg(duty), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failobject = duty.%failobject_start%;
  failtext  "Coll: Late informed sby callout ends >2h after publ";
  remark "(CCT) Resched: Rescheduled to standby, late inform, callout late end",
  planner "The rule checks that production following callout from standby that"
          " replaced cancelled production less than 48 hours before planned"
          " check-in ends no later than 2 hours after"
          " the published end of the cancelled sling."
          " The rule is only valid for FCLH.";
end

%trip_notification_deadline_sby_callout_ends_late% =
  min(duty(trip),
        if %resched_sby_callout_ends_too_late_valid%
        then rescheduling.%trip_inf_checkin% - 48:00
        else 31Dec2099);

/*
** Rule: R407
*/

rule resched_more_than_2_f_removed_FC =
  valid rescheduling.%trip_is_long_haul%
    and not trip.%is_freeday%
    and rescheduling.%trip_inf_any_freeday%
    and not crew.%is_leased_trip_start%
    and rule_exceptions.%rule_on%(trip.%start_UTC%);
    
  rescheduling.%trip_inf_freedays% - %_remaining_freedays% <= 2;
    
  startdate = trip.%start_UTC%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failobject = trip.%failobject_start%;
  failtext(Int value, Int limit) =
    rules.%failtext_int%("Coll: F-days removed from publ block",value,limit);  
  remark "(CCT) Resched: No more than two F-days may be removed from published block",
  planner "The rule checks that no more than two F-days in a production block"
          " are removed by rescheduling. The rule is only valid for FCLH.";
end

%_remaining_freedays% =
  let start_hb = rescheduling.%trip_inf_freeday_start_date_hb%;
  count(times(rescheduling.%trip_inf_freedays%)) where
    (rescheduling.%is_freeday_date_ix%(start_hb));

/*
** In case a working period with both SH and LH gets assigned on scheduled standby duty,
** it should not be possible to reschedule FC to check out on informed time-off day.
**
** Similar to rules_resched_cct.resched_R136_ALL but only for FC on MFF working period.
** JIRA: SKCMS-2406
*/
export rule resched_SH_LH_SBY_checkout_on_informed_timeoff =
  valid not void(rescheduling.%trip_scheduled_checkout_hb%)
    and %flight_crew_wop_SH_LH_SBY% and %is_LH_from_standby_callout% /* Opposite of rules_resched_cct.resched_R136_ALL */
    and not crew.%is_leased_trip_start%
    and not %trip_end_is_bought%
    and %trip_is_rescheduled_timeoff%
    and not %rescheduled_after_illness%
    and rule_exceptions.%rule_on%(trip.%start_UTC%);
    
    not %trip_has_checkout_on_informed_timeoff_day%;

  startdate = trip.%start_UTC%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%,%duty_lh_region%));
  failobject = trip.%failobject_end%;
  failtext "STB callout with C/O on F-day if SH in same wop not allowed";
  remark "Standby callout for LH with checkout on duty free day with SH in same working period is not allowed",
  planner "A standby callout for LH that results in a checkout on a day off is only allowed if there is only LH"
          " in the working period. If there is a combination of SH and LH then the rules for standby callout to SH"
          " applies and checkout needs to be on the same day as the standby";
end

/*
** Rescheduling for Mixed Fleet Flying (MFF)
**
** In case of SH+LH it should not be possible to reschedule
** to a later check out before a day off.
**
** JIRA: SKCMS-2381
*/

export rule resched_SH_LH_FC_later_co =
  valid crew.%is_pilot%
    and not wop.%is_freeday%
    and wop.%is_SH_LH%
    and not %is_LH_from_standby_callout%
    and rule_exceptions.%rule_on%(wop.%start_UTC%);

  wop.%end_hb% <= rescheduling.%wop_inf_sched_end_time%;

  startdate = wop.%start_UTC%;
  severity = last(leg(wop), %severity_filter_int%(leg.%start_hb%, %duty_lh_region%));
  failobject = wop.%failobject_end%;
  failtext(Abstime value, Abstime limit) =
    rules.%failtext_abs%("Coll: Rescheduled later C/O with SH+LH combination in wop not allowed", value, limit);
  remark "(CCT) Rescheduling LH to a later check-out is not allowed if same wop has a combination of SH+LH",
  planner "When rescheduling a pilot from one LH flight to another LH, and if pilot flies active on SH in the"
          " same working period, then a later checkout is not allowed for the LH flight and rescheduling rules"
          " for SH needs to be adhered to.";
end

/*
** Rule: Inform limit for when production assigned on blank day 
**       according to K15
*/

rule resched_bl_inform_in_time_FC =
  valid %resched_bl_inform_in_time_FC_valid%
	  and rule_exceptions.%rule_on%(trip.%start_UTC%);
    
  %inform_resched_bl_in_time%;
  
  startdate = trip.%start_UTC%;
  severity = first(leg(trip), %severity_filter_int%(leg.%start_hb%, %duty_lh_region%));
  failobject = trip.%failobject_start%;

  failtext  concat("Coll: Resched of BL-day informed after ", format_time(%resched_bl_inform_in_time_FC_inform_limit% , "%02H:%02M %02d%03b"));
  remark "(CCT) Resched: BL-day resched must be informed 18:00 day before if Danish crew, for other crew by end of last production duty. If BL is immediately after off-duty resched must be informed 12:00h before.",
  planner "The rule checks that rescheduling of BL-day"
          " is informed no later than 18:00 the day before or"
          " if BL-day is after off-duty act. then 12h before planned ci"
          " If easter or xmas, the crew is informed"
          " no later than 7 calendar days before at 18:00"
          " The rule is only valid for FC.";
end

%inform_resched_bl_in_time% =
  %now_hb% <= %resched_bl_inform_in_time_FC_inform_limit%;

%trip_notification_deadline_bl_resched% =
  if %resched_bl_inform_in_time_FC_valid%
  then %resched_bl_inform_in_time_FC_inform_limit%
  else void_abstime;

%resched_bl_inform_in_time_FC_inform_limit% =
    if crew.%has_agmt_group_sk_fd% then
       if %_christmas% or %_easter%
       then
         /* During easter (skärtorsdagen till annandag påsk) and
            christmas 24dec-01jan publication has to be made
            no later then 18:00 7 calendar days before BL */
         rescheduling.%trip_inf_blank_start_hb% - 6*24:00 - 06:00
       else if prev(trip(roster), trip.%category_code% = "OFF") and rescheduling.%trip_checkin_hb% < (rescheduling.%trip_inf_blank_start_hb% + 06:00) then
          rescheduling.%trip_checkin_hb% - 12:00
       else
          /* Production on BL no later than 18:00 defore BL */
          rescheduling.%trip_inf_blank_start_hb% - 06:00
    else
        rescheduling.%trip_inf_blank_start_hb% - 06:00;

%resched_bl_inform_in_time_FC_valid% =
  trip.%is_on_duty% and
  not crew.%is_leased_trip_start% and
  default(first(duty(trip), %duty_is_rescheduled_blank_day_at_start_hb%), false) and
  not %trip_standby_callout_with_overnight_into_bl%;


/* end of file */
