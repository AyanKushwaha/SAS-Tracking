/* -*- crc -*-
 *
 * Models costs of lifestyle satisfaction
 *
 */
root module lifestyle_cost

import fundamental;
import iterators;
import lifestyle;


export %cost% =
 if (%factor% = 0) then 0
 else %factor% * sum(times(1), %cost_element%(%times_index_0%));

export %cost_element%(int element) =
  if (%factor% = 0) then
    0
  else
    %cost_prod_element%(element);

table cost_pbs(int element) =
  element -> export %name_element%,
             %cost_prod_element%;
  1 -> "Lifestyle bids cost",
    %lifestyle_bids_cost%;
  - -> "", 0;
end

export %factor% =
  parameter 10
  remark "6.0      Weight of LIFESTYLE";

%linear_coefficient% =
    parameter 1
    remark "Linear coefficient for reward per point above target";

%quadratic_coefficient% =
    parameter 1
    remark "Quadratic coefficient for cost per bid point below target";

%lifestyle_bids_cost% =
    (%cost_below_target% - %reward_above_target%) / %_diminishing_factor%;

/* restrict the weight of lifestyle */
%_diminishing_factor% = 10;

%cost_below_target% =
    %quadratic_cost_below_target% + %linear_cost_below_target%;

%reward_above_target% =
    %linear_coefficient% * %points_above_target%;


%quadratic_cost_below_target% =
    %quadratic_coefficient% * %points_below_target% * %points_below_target%;

%linear_cost_below_target% =
    %linear_coefficient% * %points_below_target%;

%points_below_target% =
    nmax(%points_below_target_raw%, 0);

%points_above_target% =
    nmax(%points_above_target_raw%, 0);

export %crew_is_below_target% =
    %points_below_target_raw% > 0;

%points_below_target_raw% =
    %target% - lifestyle.%crew_fulfillment%;

export %points_above_target_raw% =
    lifestyle.%crew_fulfillment% - %target%;

%target% =
    parameter 100;
