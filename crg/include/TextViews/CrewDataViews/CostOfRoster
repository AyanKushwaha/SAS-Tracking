/* $Header$
 *
 * Default configuration of a crew data view. The original of
 * this file can be found in
 * $CARMSYS/carmusr_default/crg/include/TextViews/CrewDataViews/Default
 *
 * When creating a new configuration report it can be useful to
 * put a soft link to the report in file in $CARMUSR/crg/crew_window_general/
 * and let the softlink name end with .output. Then you can access
 * it from Generate Report in the assignment window or in the
 * crew data window and study the text output from the report.
 */

options
  /* Default width is 80 characters. A crew configuration
     may produce longer lines than that. */
  ascwidth=500;
end

crew_rows:
box
    repeat foreach iterators.roster_set;
    make
      repeat 10 times;
      make
        if roster_cost.%section_exist_ix% then
          ce: text 12 right "%D", 
             default(crg_basic.%print_figure%(roster_cost.%section_cost_ix%), "VOID");
        end
      relate
        if roster_cost.%section_exist_ix% then
          row +ce, ce;      
        end
      end
      crew_row: row (text 8 left "%s", crew.%id%), 
          	(text min 15x1 max 15x1 left "%D", crew.%surname%), 
               	(text min 12x1 max 12x1 left bold "%D", roster_cost.%roster%),
                -+ce;
    relate
      column +crew_row, crew_row;
    end
    crew_rows: column -+crew_row;
    
    repeat 10 times;
    make
      if roster_cost.%section_exist_ix% then
        t: text min 12x1 max 12x1  left "%D", roster_cost.%section_name_ix%;
      end
    relate
      if roster_cost.%section_exist_ix% then
        row +t, t;
      end
    end
    title_row: row -+t;

    header: row 
        (text 8 "Crew id"), 
        (text 15 "Surname"), 
        (text 10 min 12x1 max 12x1 left bold "Total"),
        title_row;

    column header, crew_rows;
end


