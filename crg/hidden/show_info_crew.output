/* -*- crg -*- 
 * 
 * $Header$
 * 
 * Dynamic report for crew header
 *
 * Major changes:
 *   Completely rebuilt according to spec in PS07, also changed all variables to use crg_info module
 *   to control void-behavior. /EkG, 20070510
 *******************************************************/

options
  text = noframe;
  ascwidth = 130;
  ascheight = 4;
end

training:
box
  column
      (row (text "%s", crg_info.%training_row1%),*),
      (row (text "%s", crg_info.%training_row2%),*),
      (row (text "%s", crg_info.%training_row3%),*),
      (row (text "%s", crg_info.%training_row4%),*);
end

all_info:
box
    if default(crg_info.%crew_is_new%,false) and default(crg_info.%is_pilot%, false) then
        check_info: text "NEW: %-6s",
            default(crg_info.%block_hours_since_last_check_now%,"00:00");
    else
        check_info: text "";
    end
	
    if default(crg_info.%is_pilot%, false) then
        wesim_info: text "%-23s " ,  default(crg_info.%last_performed_sim%, "");
    else
        wesim_info: text "Last weekend: %-10s", default(crg_info.%last_weekend%,"");
    end
    
    if not default(annotations.%on_crew%, false) then
	    annot_info: text "Annotations: No current - none coming in 30 days %-15s %s", " ","";
	elseif default(length(crg_info.%annotations%), 0) < 70 then
        annot_info: text "Annotations: %-14s %-36s %s", default(crg_info.%annotations%, ""), " ","";
    else
        annot_info: text "Annotations: %-.66s...", default(crg_info.%annotations%, "");
    end
	
    row (
      column (
        row (
          text "%-24.24s %-5s (%-5s)  %-4s %-10s %-8s %-3s Sen: %-5s", default(crg_info.%crew_name%,""),
          default(crg_info.%empno%,""),
          default(crg_info.%crew_id%,""),
          default(crg_info.%rank%,""),
          default(crg_info.%part_time_factor%,""),
          default(crg_info.%group%,""),
          concat(default(crg_info.%homebase%,""),"/",default(crg_info.%crew_planning_group%,"")),
          default(crg_info.%seniority%,"")
        ),
        *
      ), (
        row (
          text "AC Quals: %-14s %-39s %s", 
	  default(crg_info.%ac_quals%, ""),
          default(crg_info.%fdays_phone%, ""),
	  default(crg_info.%freedaytarget_proddays_info%, "")
        ),
        check_info,
        *
      ), (
        row wesim_info,
        (
          text " Last flown: %-36s%-10s",
          concat(default(crg_info.%last_flown%,""),", ",default(crg_info.%last_standby_line_str%,"")),
          default(crg_info.%agmt_group%,"")
        ),
        *
      ), (
        row annot_info, 
        *
      )
    ), (
      box training
    );
end

show_annotations:
box

  if annotations.%annotation_active% <= 2 then
    anno_row_1: text MAX 86x1 "%-40s      %-40s", 
	  default(annotations.%anno_str_1_sub_1%, ""), default(annotations.%anno_str_2_sub_1%, "");
	  
    anno_row_2: text "%-40s      %-40s", 
	  default(annotations.%anno_str_1_sub_2%, ""), default(annotations.%anno_str_2_sub_2%, "");
	  
    anno_row_3: text " ";
	
    anno_row_4: text "Valid from %s to %-23s %s %s",
	  default(format_time(annotations.%annotation_period_start%(1), "%02d%02b%02y"), ""),
	  default(format_time(annotations.%annotation_period_end%(1), "%02d%02b%02y"), ""),
	  default(format_time(annotations.%annotation_period_start%(2), "Valid from %02d%02b%02y to"), ""),
	  default(format_time(annotations.%annotation_period_end%(2), "%02d%02b%02y"), "");
	  
  else
    anno_row_1: text MAX 40x1 "%-40s", default(annotations.%anno_str_1_sub_1%, "");
	anno_row_2: text MAX 40x1 "%-40s", default(annotations.%anno_str_1_sub_2%, "");
	anno_row_3: text " ";
	anno_row_4: text "Valid from %s to %s   Coming: %s",
	  default(format_time(annotations.%annotation_period_start%(1), "%02d%02b%02y"), ""),
	  default(format_time(annotations.%annotation_period_end%(1), "%02d%02b%02y"), ""),
	  default(annotations.%pp_codes_with_dates%, "");
  end	

  row  
  (column
    (row anno_row_1,*), (row anno_row_2,*), (row anno_row_3,*), (row anno_row_4,*));

end
	 
box
  repeat foreach default_context; 
  make
    if default(annotations.%on_crew%, false) and studio_config.%annotations_view_toggle% then
      box show_annotations;
    else 
      box all_info;
    end
	/*box all_info;*/
  end
end
