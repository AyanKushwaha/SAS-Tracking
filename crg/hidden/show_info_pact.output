/* -*- crg -*- 
**
**	$Header$
**
**	Report for showing personal activity data in info-window.
**
 */

options
    text = noframe;
    ascwidth = 100;
    ascheight = 4;
end

leftColumn: 
box
    column
	(row (text "%-11s at %-3s   ",
	    crg_info.%pact_ground_name%, crg_info.%airport%), *),
	(row (text "%-6s %-11s",
	    crg_info.%time_h1%, crg_info.%time_1%), *),
	(row (text "%-6s %-11s",
	    crg_info.%time_h2%, crg_info.%time_2%), *),
	(row (text "%-6s %-11s",
	    crg_info.%changes_h%, crg_info.%changes%), *);
end

midColumn: 
box
    column
	(row (text "LEG  FLT %5s  CNX %5s   FDP %5D   ",
	    crg_info.%leg_time%, crg_info.%leg_cnx%, crg_duty_points.%fdp_until_now%), *),
	(row (text "DUTY DUT %5s  CNX %5s  REST %5s",
	    crg_info.%duty_time%, crg_info.%duty_cnx%, crg_info.%duty_rest%), *),
	(row (text "TRIP DUT %5s   CI %5s    CO %5s",
	    crg_info.%trip_time%, crg_info.%trip_ci%, crg_info.%trip_co%), *),
	(row (text "WOP  DUT %5s      %5s  DP7d %5s",
	    crg_info.%wop_time%, " ", crg_duty_points.%dp_7_days_bwd_str%), *);
end

rightColumn: 
box
	if crg_info.%duty_is_privately_traded% then
    	privately_traded: text "%s", crg_info.%privately_traded_leg_text%;
    else
    	privately_traded: text " ";
    end

    column
	(row (text "%-4s %-2s %-4s Pos: %-2s %-5s",
            "", "", "",
	    crg_info.%pos%, crg_info.%duty_code%), *),
	(row (text "%s", crg_info.%task_descr%), *),
	(row privately_traded, *),
	(row (text " "), *);
end

box
  repeat foreach default_context; 
  make
    row
    (box leftColumn),
    (box midColumn),
    (box rightColumn);
  end
end

