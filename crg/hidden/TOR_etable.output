    /* -*- crg -*- */
/******************************************************

$Header$

-------------------------------------------------------

Use:
    This report generates an external table with all 
    connections in the current context. Used by the TOR 
    module.

-------------------------------------------------------
Created:    2003-04-07
By:         Thomas Rudolfsson
*******************************************************/

options
  ascwidth = 116;
  ascii    = asctrimpage asctrimline;
  stretch  = 100;
  text     = noframe fixed nospace left; 
end

tbl_def:   BOX
   repeat foreach default_context;
   make  
     com1:   TEXT "/*\n ** TOR - Connection information **";
     com2:   TEXT " From Plan: %D/%D/%D/%D",   fp_name,fp_version,lp_name,sp_name;
     com3:   TEXT " Produced : %D at %D by %D",studio_tor.%tor_date%,studio_tor.%tor_time%,user;
     com4:   TEXT " Name of etable when created: ETABLE_INIT_NAME\n*/";
     numcol: TEXT "9";
     col1:   TEXT "Sstation_1 \" Airport 1\"   0 / 6,";
     col2:   TEXT "Sfrom_id \"From Leg\"  0 / 10,";
     col3:   TEXT "Sfrom_dh \"From Deadhead\"  1 / 12,";
     col4:   TEXT "Ifrom_traffic_day \"From Local Traffic Day\"  3 / 15,";
     col5:   TEXT "Sstation_2 \" Airport 2\"   3 / 6,";
     col6:   TEXT "Sto_id \"To Leg\"  0 / 10,";
     col7:   TEXT "Sto_dh \"To Deadhead\"  1 / 10,";
     col8:   TEXT "Ito_traffic_day \"To Local Traffic Day\"  3 / 15,"; 
     col9:   TEXT "Istop_type \"Kind of Connection\"  ? \"0 -> inside RTD, -1 -> start/end of CRR, >0 -> between RTDs\"  3 / 16,"; 
     r1: TEXT RIGHT MIN  6 "\"%D\", ", "USE_WEEKDAY";
     r2: TEXT RIGHT MIN 15 "\"%D\", ", studio_tor.%tor_write_weekdays_in_etable%;
     r3: TEXT RIGHT MIN  7 "\"%D\", ", "-";
     r4: TEXT RIGHT MIN  6 "%D, "    , 0;
     r5: TEXT RIGHT MIN 15 "\"%D\", ", "-";
     r6: TEXT RIGHT MIN 15 "\"%D\", ", "-";
     r7: TEXT RIGHT MIN  7 "\"%D\", ", "-";
     r8: TEXT RIGHT MIN  6 "%D, "    , 0;
     r9: TEXT RIGHT MIN  6 "%D, "    , 0;
     rx: row r1,r2,r3,r4,r5,r6,r7,r8,r9,*;
    
     COLUMN com1,com2,com3,com4,numcol,col1,col2,col3,col4,col5,col6,col7,col8,col9,rx;
   end 
END

detrow_normal: BOX
        r1: TEXT RIGHT MIN  7 "\"%D\", ", tor.%tor_end_station%;
        r2: TEXT RIGHT MIN 15 "\"%D\", ", tor.%tor_id%;
        r3: TEXT RIGHT MIN  7 "\"%D\", ", tor.%tor_dh%;
        r4: TEXT RIGHT MIN  6 "%D, "    , studio_tor.%tor_dep_traffic_day_write%;
        r5: TEXT RIGHT MIN  7 "\"%D\", ", tor.%tor_next_dep_station%;
        r6: TEXT RIGHT MIN 15 "\"%D\", ", tor.%tor_next_id%;
        r7: TEXT RIGHT MIN  7 "\"%D\", ", tor.%tor_next_dh%;
        r8: TEXT RIGHT MIN  6 "%D, "    , studio_tor.%tor_next_leg_dep_traffic_day_write%;
        r9: TEXT RIGHT MIN  6 "%D, "    , tor.%tor_stop_type%;
        
        ROW r1,r2,r3,r4,r5,r6,r7,r8,r9,*;
     
END

    
detrow_first: BOX
        r1: TEXT RIGHT MIN  7 "\"%D\", ", "-";
        r2: TEXT RIGHT MIN 15 "\"%D\", ", "-";
        r3: TEXT RIGHT MIN  7 "\"%D\", ", "-";
        r4: TEXT RIGHT MIN  6 "%D, "    , "-1";
        r5: TEXT RIGHT MIN  7 "\"%D\", ", tor.%tor_start_station%;
        r6: TEXT RIGHT MIN 15 "\"%D\", ", tor.%tor_id%;
        r7: TEXT RIGHT MIN  7 "\"%D\", ", tor.%tor_dh%;
        r8: TEXT RIGHT MIN  6 "%D, "    , studio_tor.%tor_dep_traffic_day_write%;   
        r9: TEXT RIGHT MIN  6 "%D, "    , "-1";

        ROW r1,r2,r3,r4,r5,r6,r7,r8,r9,*;
END 


connections: BOX
  REPEAT FOREACH iterators.leg_set;
  WHERE tor.%tor_write_connection%;
  MAKE
        if tor.%tor_write_first_leg_in_chain% then 
          s1: ROW (BOX detrow_first),*;
        else
          s1: ROW *,*;
        end
    
          s: COLUMN s1,(ROW (BOX detrow_normal),*);

  RELATE  
     
     COLUMN +s,NEWPAGE,s; 
  
  END

END


etab: BOX /* main box */
   
   tbl: COLUMN (BOX tbl_def),(BOX connections);
   
END
