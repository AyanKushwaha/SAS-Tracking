/* -*- crg -*- 
 * $Header$

 Create crew position dependent menues

 *****************************************************/

options   
  text = noframe;
  ascii = asctrimpage; 
  ascwidth = 2048;
end

define(NameOfReport, menu_scripts/std_menues/AssignMenues)
include(CRGInclude/Etab_header.inc)

etab_header_bottom: 
box
 fd: column 
  (text "   (generated) CREW POSITIONS MENU"),
  (text ""),
  (text "   -------------------------------------------------------"),
  (text ""),
  (text "   This file should be included from"),
  (text "     menu_scripts/menu_extensions"),
  (text ""),
  (text "\*\/");
  column fd, (text noframe 0x1 ""), *;
end

/* Build a menu with category and function as components */
cat_func_menu: 
box(title, add_separators, cmd_head, cmd_sep, cmd_tail)
    repeat 12 times;
    make
      if (crew_pos.%pos2used%(fundamental.%py_index%)) then
        crew_pos: 
          row (text 20x1 "  \"%D\"",
                 crew_pos.%pos2func_name%(fundamental.%py_index%)),
              (text "%D\"%D\"%D\"%D\"%D", 
                 cmd_head, 
                 crew_pos.%pos2cat%(fundamental.%py_index%),
                 cmd_sep, 
                 crew_pos.%pos2func%(fundamental.%py_index%),
                 cmd_tail);
      end
    relate
      column +crew_pos,page,crew_pos;
    end

    if (add_separators = "Y") then
      rows: column (text "  f.separator"), -+crew_pos, (text "  f.separator");
    else
      rows: column -+crew_pos;
    end

    crew_pos_menu:
      column (text "Menu %D %D", title, crg_basic.%curly_left_brace%), 
             rows,
             (text "%D", crg_basic.%curly_right_brace%),
             (text 1x1 "");

end

/* Build a select menu with function as components */
func_select_menu: 
box(title, add_separators)
    repeat 12 times;
    make
      if (crew_pos.%pos2used%(fundamental.%py_index%)) then
        crew_pos: 
          row (text 20x1 "  \"%D\"",
                 crew_pos.%pos2func_name%(fundamental.%py_index%)),
              (text "REDO f.exec PythonEvalExpr(\"MenuCommandsExt.select(%D'crew.rank':'%D'%D)\")", 
                 crg_basic.%curly_left_brace%,
                 crew_pos.%pos2func%(fundamental.%py_index%),
                 crg_basic.%curly_right_brace%);
      end
    relate
      column +crew_pos,page,crew_pos;
    end

    if (add_separators = "Y") then
      rows: column (text "  f.separator"), -+crew_pos, (text "  f.separator");
    else
      rows: column -+crew_pos;
    end

    crew_pos_menu:
      column (text "Menu %D %D", title, crg_basic.%curly_left_brace%), 
             rows,
             (text "%D", crg_basic.%curly_right_brace%),
             (text 1x1 "");

end

main: box
  repeat foreach default_context;
  make

    column 
         (row (box etab_header_top), *),
         (row (box etab_header_bottom), *),
         newpage,
         (row (box cat_func_menu("CopyCrrTo", "N",
                "f.exec gpc_copy_trip(gpc_info, ", ", ", ", 0)")), *),
         newpage,
         (row (box cat_func_menu("ChangePos", "N", 
                "f.exec CuiChangeAssignedPosition(gpc_info, ", ", ", ")")), *),
         newpage,
         (row (box cat_func_menu("AssignPosMenu", "N", 
                "f.exec CuiAssignCrr(gpc_info, ", ", ", ",0)")), *),
         newpage,
         (row (box cat_func_menu("AssignableCrewPosMenu", "N", 
                "f.exec CuiFindAssignableCrew(gpc_info, ", ", ", ", \"SUB_PLAN\")")), *),
         newpage,
         (row (box cat_func_menu("AssignableCrewPosWinMenu", "N", 
                "f.exec CuiFindAssignableCrew(gpc_info, ", ", ", ", \"WINDOW\")")), *),
         newpage,
         (row (box cat_func_menu("AssignablePosMenu", "N", 
                "f.exec CuiFindAssignableCrr(gpc_info, ", ", ", ", \"SUB_PLAN\")")), *),
         newpage,
         (row (box cat_func_menu("AssignablePosWinMenu", "N", 
                "f.exec CuiFindAssignableCrr(gpc_info, ", ", ", ", \"WINDOW\")")), *),
         newpage,
         (row (box func_select_menu("PredefinedCrewSelect", "Y")), *),
         (text 1x1 "");
  end
end
