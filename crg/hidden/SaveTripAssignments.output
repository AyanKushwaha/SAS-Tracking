/* -*- crg -*- 
$Header$ */ 
/*****************************************************
** The report saves all Pre-assigned trips to an etab, 
** by picking  trips available in the window, The etable 
** contain information of first flight id, departure station
** and departure time (utc).
*****************************************************/

options   
  text = noframe;
  ascii = asctrimpage; 
  ascheight = 1100;
  ascwidth = 300;
end

define(NameOfReport, Trip Preassignments Etab)
include(CRGInclude/Etab_header.inc)

main:
box
    repeat foreach default_context;
    make

        repeat foreach iterators.roster_set;
        where fundamental.%is_roster%;
	make
	    repeat foreach iterators.trip_set;
	    where not trip.%is_environment% 
                  and not trip.%is_pact%
                  and trip.%is_locked% 
                  and trip.%start_lt% >= fundamental.%pp_start%;
	    make
		one_trip: text left "\"%D\", %D, \"%D\", \"%D\", %D, \"%D\",",
		    crew.%id%,
		    index(0), 
		    crew_pos.%pos2func%(assigned_function+1),
		    studio_assign.%trip_unique_string%,
		    crg_date.%etab_abs%(studio_assign.%trip_departure_time_lt%),
                    studio_assign.%trip_working_code%;

	    relate
		column +one_trip, one_trip;
	    end

	    one_crew: column -+one_trip;
        
	relate
	    column +one_crew,one_crew;
	end

    column
	(row (box etab_header_top),*),
	(text left "*/"),
	(text left "6"),
	(text left "SCrewId,"),
        (text left "IIndex1,"),
	(text left "SCrewFunc,"),
	(text left "STripDesc,"),
	(text left "AStartLocal,"),
	(text left "SWorkCD,"), 
	(text left " "), 
	-+one_crew;
    end
end

