/* -*- crg -*- */ 

position_base: box
  repeat crg_base_constraints.%bc_crg_pa_num_days% times;
  make
    if crg_base_constraints.%bc_crg_pa_valid_date% then
      f1:text 5 "%D", crg_base_constraints.%bc_crg_pa_day_production%;
    else
      f1:text 5 "-";
    end
    if crg_base_constraints.%bc_crg_pa_week_start% then
      s1:text "%D", crg_base_constraints.%bc_crg_pa_date_string%;
      row s1, !, f1;
    end
    if crg_base_constraints.%bc_crg_pa_week_end% then
      e1:text 12 left " %7D", 
	crg_base_constraints.%bc_crg_pa_week_total_production%;
      row f1, !, e1;
    end
  relate
    if crg_base_constraints.%bc_crg_pa_week_start%
    and crg_base_constraints.%bc_crg_pa_week_end% then
      column +s1, s1;
      column +e1, e1;
    elseif crg_base_constraints.%bc_crg_pa_week_start% then
      column +s1, s1;
    elseif crg_base_constraints.%bc_crg_pa_week_end% then
      row +f1, f1;
      column +e1, e1;
    else
      row +f1, f1;
    end
  end
  first_field: text left " Crew pos: %-3D ", 
    base_constraints.%bc_crg_pa_position_name%;
  head: row first_field, !, (text 5 "MON"), 
	    (text 5 "TUE"), (text 5 "WED"), (text 5 "THU"),
	    (text 5 "FRI"), (text 5 "SAT"), (text 5 "SUN"), !, *;

  first_row: row -s1, *, -e1;
  column head, !, first_row;
  align [right] first_field, -s1;

  last_row: row +s1, *, +e1;
  column last_row, !, 
    (text left top 1x2 " Total production : %D", 
	crg_base_constraints.%bc_crg_pa_total_production%);
end

base: box
  repeat 13 times;
  make
    if crg_base_constraints.%bc_crg_pa_position_used% then
      pb1: box position_base;
    end
  relate
    if crg_base_constraints.%bc_crg_pa_position_used% then
      column +pb1, pb1;
    end
  end
end


bc: box
	base_name: text 6 "%D", crg_base_constraints.%bc_crg_bc_base%;
	pos: text 5 "%D", crg_base_constraints.%bc_crg_bc_position%;
	if base_constraints.%bc_is_dated_mode% then
	  period_start: text 10 "%D", 
	    crg_base_constraints.%bc_crg_bc_period_start%;
	  period_end: text 10 "%D", 
	    crg_base_constraints.%bc_crg_bc_period_end%;
	end
	min_daily: text 8 "%D", 
	    crg_base_constraints.%bc_crg_bc_min_daily_prod%;
	max_daily: text 8 "%D", 
	    crg_base_constraints.%bc_crg_bc_max_daily_prod%;
	min_total: text 8 "%D", 
	    crg_base_constraints.%bc_crg_bc_min_total_prod%;
	max_total: text 8 "%D", 
	    crg_base_constraints.%bc_crg_bc_max_total_prod%;
	min_total_ratio: text 8 "%D", 
	    crg_base_constraints.%bc_crg_bc_min_total_prod_ratio%;
	max_total_ratio: text 8 "%D", 
	    crg_base_constraints.%bc_crg_bc_max_total_prod_ratio%;
	if base_constraints.%bc_is_dated_mode% then
	  row base_name, pos, period_start, period_end,
	    min_daily, max_daily, min_total, max_total, 
	    min_total_ratio, max_total_ratio;
	else
	  row base_name, pos,
	    min_daily, max_daily, min_total, max_total, 
	    min_total_ratio, max_total_ratio;
	end
end

bc_main: box
  repeat foreach default_context;
  make
    repeat crg_base_constraints.%bc_num_base_constraints% times;
    make
      if crg_base_constraints.%bc_crg_bc_constr_on% then
	bc_box : row (box bc), *;
      end
    relate
      if crg_base_constraints.%bc_crg_bc_constr_on% then
	column +bc_box, bc_box;
      end
    end

    if base_constraints.%bc_is_dated_mode% then
      bc_header: row (text 6 "Base"), (text 5 "Pos"), (text 10 "Start"), 
	(text 10 "End"), (text 8 "DayMin"), (text 8 "DayMax"), 
	(text 8 "TotMin"), (text 8 "TotMax"), (text 8 "%%Min"), 
	(text 7 "%%Max"), *;
    else
      bc_header: row (text 6 "Base"), (text 5 "Pos"), (text 8 "DayMin"), 
	(text 8 "DayMax"), (text 8 "TotMin"), (text 8 "TotMax"), 
	(text 8 "%%Min"), (text 7 "%%Max"), *;
    end

    column (text " "), (text left "Base Constraints Add-in Package Report"), 
	(text " "), bc_header, !, -+bc_box;

    repeat crg_base_constraints.%bc_num_bases% times;
    make
      if crg_base_constraints.%bc_crg_pa_active% then
	base_box: column (text left top 1x2 "Base %D", 
		crg_base_constraints.%bc_crg_pa_base%), (row (box base), *);
      end
    relate
      if crg_base_constraints.%bc_crg_pa_active% then
	column +base_box, page, base_box;
      end
    end
    column +bc_box, (text 1x2 ""), 
	(text left top 1x2 "Base Production Analysis from Add-in Package"), 
	-base_box;
  end
end
