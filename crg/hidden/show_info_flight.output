/* -*- crg -*- 
 *
 * $Header$
 *
 * Purpose:
 *   Report for showing flight-data in info-window.
 * 
 * Created by:
 *   Extracted from the Carmen Standard User, 07-Dec-2005
 *
 * Major changes:
 *   Adapted the report to match SAS crew positions and adjusted the layout, 
 *   HenrikA 12-Dec-2005
 *   Completely rebuilt according to spec in PS07, also changed all variables to 
 *   use crg_info module to control void-behavior. /EkG, 20070510
 *    
 */

options
  text = noframe;
  ascwidth = 120;
  ascheight = 4;
end

leftColumn: 
box
    column
	(row (text "%-6s %-11s   ",
	    crg_info.%flight_name%, crg_info.%airports%), *),
	(row (text "%-6s %-11s",
	    crg_info.%time_h1%, crg_info.%time_1%), *),
	(row (text "%-6s %-11s",
	    crg_info.%time_h2%, crg_info.%time_2%), *),
	(row (text "%-6s %-11s",
	    crg_info.%changes_h%, crg_info.%changes%), *);
end

midColumn: 
box
    column
	(row (text "LEG  FLT %5s  CNX %5s   FDP %5D  ",
	    crg_info.%leg_time%, crg_info.%leg_cnx%, crg_duty_points.%fdp_until_now%), *),
	(row (text "DUTY DUT %5s  CNX %5s  REST %5s",
	    crg_info.%duty_time%, crg_info.%duty_cnx%, crg_info.%duty_rest%), *),
	(row (text "TRIP DUT %5s   CI %5s    CO %5s",
	    crg_info.%trip_time%, crg_info.%trip_ci%, crg_info.%trip_co%), *),
	(row (text "WOP  DUT %5s    %5s%s  DP7d %5s",
	    crg_info.%wop_time%, " ", crg_info.%long_or_short_haul%, crg_duty_points.%dp_7_days_bwd_str%), *);
end

rightColumn: 
box
    column
	(row (text "%-4s %-2s %-4s Pos: %-2s %-s",
	    crg_info.%ac_type%, crg_info.%service_type%, crg_info.%planning_group%,
	    crg_info.%pos%, default(crg_info.%duty_code%,"")), *),
	(row (text "%s", crg_info.%rc_line2%), *),
	(row (text "%s", crg_info.%rc_line3%), *),
	(row (text "%s", crg_info.%rc_line4%), *);
end
   
anotherColumn: 
box
    if crg_info.%crew_is_new% and default(crg_info.%is_pilot%, false) then
        check_info: text "NEW: %D", 
            crg_info.%block_hours_since_last_check_leg_end%;
    else
        check_info: text " ";
    end
    if crg_info.%duty_is_privately_traded% then
    	privately_traded: text "%s", default(crg_info.%privately_traded_leg_text%, "");
    else
    	privately_traded: text " ";
    end
    column 
        (row privately_traded, *),
        (row check_info, *),
        (row (text " "), *),
        *;
end

box
  repeat foreach default_context; 
  make
    row
    (box leftColumn),
    (box midColumn),
    (box rightColumn),
    (box anotherColumn);
  end
end

