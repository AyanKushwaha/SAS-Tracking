/* -*- crg -*- *
*
* $Header$
*
*******************************************************
FAIRNESS REPORTS
--------------------------------------------------------
 
Use:
    This rule file is used by the reports CrewFairness and
    FairnessStatistics.
 
Requirement:
 
--------------------------------------------------------
Author:  Sami Spjuth 
Created: 19Aug99
*******************************************************/

define(RowsPerPage,10)
define(cellWidth,9)

define(NameOfReport,"Fairness Statistics")
include(CRGInclude/typeset_standard.inc)

options
  text = noframe fixed novspace;
  stretch=100;
end

/*
** MAIN BOX
*/

main:
box
    repeat foreach studio_fairness.crew_fairness_group_set;
    sort fairness.%fairness_crew_group%;
    make
        if (ix-1) mod RowsPerPage = 0 then
          table_header:  
	    row
	      (Helem:  HTEXT "Element"),*,
	      (Hft:    HTEXT right "100%% Target"),
	      (Hat:    HTEXT right "Avg Target"),
	      (Har:    HTEXT right "Avg Roster"),
	      (Had:    HTEXT right "Avg Dev"),
	      (Hmd:    HTEXT right "Mean Dev"),
	      (Hsd:    HTEXT right "Std. Dev"),
	      (Hmaxd:  HTEXT right "Max Dev"),
	      (Hac:    HTEXT right "Avg cost"),
	      (Htc:    HTEXT right "Total cost");
        end

        table_item: 
          column 
  	    (text "%s (%D)",
		fairness.%fairness_crew_group%,
	        studio_fairness.%nr_crew%),!,
	    PADSMALL, -+element,!, PADFULL;
	align [left,right] -elem,Helem -ft,Hft -at,Hat
                           -ar,Har -ad,Had -md,Hmd -sd,Hsd -maxd,Hmaxd 
			   -ac,Hac -tc,Htc;

	repeat 40 times;
	make
	    if fairness.%distribute_ix% and studio_fairness.%any_in_crew_group_has_ix% then
		element:
		row
		    (elem:  text  "%s", fairness.%text_ix%),*,
		    (ft:    text cellWidth right "%D",studio_fairness.%full_target_ix%),
		    (at:    text cellWidth right "%D",studio_fairness.%avg_target_ix%),
		    (ar:    text cellWidth right "%D",studio_fairness.%avg_roster_ix%),
		    (ad:    text cellWidth right "%D",studio_fairness.%avg_dev_ix%),
		    (md:    text cellWidth right "%D",studio_fairness.%mean_dev_ix%),
		    (sd:    text cellWidth right "%D",studio_fairness.%std_dev_ix%),
		    (maxd:  text cellWidth right "%D",studio_fairness.%max_dev_ix%),
		    (ac:    text cellWidth right "%D",studio_fairness.%avg_cost_ix%),
		    (tc:    text cellWidth right "%D",studio_fairness.%cost_ix%);
	    end
	relate
	    column +element,element;
	end

        if ix = 1 then
          c: column table_header, PADFULL, table_item;
        elseif (ix-1) mod RowsPerPage = 0 then
          h1: text prop bold "";
          c: column PADSMALL, newpage, h1, PADFULL, table_header,
                PAD, table_item;
        elseif (ix-1) mod 5  = 0 then
          c: column (text " "),table_item;
        else
          c: column table_item;
        end
    relate
	column +c, c;
    end
    column -+c, (tag "EndOfReport");
end
/* end of file */
